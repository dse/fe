#!/usr/bin/env perl
use warnings;
use strict;
use v5.10.0;
use utf8;

#------------------------------------------------------------------------------
use File::Basename qw(basename);
our $PROGNAME = basename($0);

#------------------------------------------------------------------------------
use open IO => qw(:locale);
use Encode::Locale;
use Encode qw(decode);
BEGIN {
    @ARGV = map { decode('locale', $_, Encode::FB_CROAK) } @ARGV;
}

#------------------------------------------------------------------------------
# options
our $context        = 0;
our $lineNumber     = 0;
our $initialTab     = 0;
our $kitty          = 0;
our $numberNonBlank = 0;        # a la kitty

#------------------------------------------------------------------------------
# process options
use Getopt::Long;
Getopt::Long::Configure('bundling', 'gnu_compat', 'no_ignore_case');
Getopt::Long::GetOptions(
    'help'            => sub { usage(); exit(0); },
    'context'         => \$context,
    'line-number'     => \$lineNumber,
    'initial-tab'     => \$initialTab,
    'kitty'           => \$kitty,
    'number-nonblank' => \$numberNonBlank,
) or die("Type '$PROGNAME --help' for help.\n");

sub usage { print(<<"END"); }
usage:
    $PROGNAME [<option> ...] [<filename> ...]
options:
        --help                  Display this message
END

###############################################################################

my $pp = My::IronSearcher::PostProcessor->new();

$pp->context($context);
$pp->lineNumber($lineNumber);
$pp->initialTab($initialTab);
$pp->kitty($kitty);
$pp->numberNonBlank($numberNonBlank);

$pp->run();

###############################################################################

package My::IronSearcher::PostProcessor {
    use Moo;
    INIT {
        has context        => (is => 'rw');
        has lineNumber     => (is => 'rw');
        has initialTab     => (is => 'rw');
        has kitty          => (is => 'rw');
        has numberNonBlank => (is => 'rw');
    }
    sub run {
        my $self = shift;
        my $prevFilename;
        my $lastPrefix;
        my $separatorFlag = 0;

        my $FILENAME;
        my $RESET;

        if (-t 1) {
            $FILENAME = "\e[0;32m";
            $RESET = "\e[0m";
        } else {
            $FILENAME = "";
            $RESET = "";
        }

        while (<>) {
            s{\R\z}{};

            if ($context) {
                if ($_ eq '--') {
                    $separatorFlag = 1;
                    next;
                }
            }

            unless (s{^(.*?)\t}{}) {
                print "$_\n";
                next;
            }

            my $prefix = $1;
            my $contextChar;
            if ($prefix =~ s{([-:])$}{}) {
                $contextChar = $1;
            }

            my $lineNumber;
            if ($self->lineNumber) {
                if ($prefix =~ s{\Q$contextChar\E\s*(\d+)$}{}) {
                    $lineNumber = $1;
                }
            }

            if (!defined $lastPrefix || $lastPrefix ne $prefix) {
                $separatorFlag = 0;
                if ($self->kitty) {
                    print "${FILENAME}==> $prefix <==${RESET}\n";
                } else {
                    print "${FILENAME}$prefix${RESET}\n";
                }
                $lastPrefix = $prefix;
            }

            if ($separatorFlag) {
                if ($self->kitty) {
                    print("\t--\n");
                } else {
                    print "    --\n";
                }
                $separatorFlag = 0;
            }

            if ($self->kitty) {
                if ($numberNonBlank) {
                    if (defined $contextChar && $contextChar ne ':') {
                        if ($contextChar eq '-') {
                            print("\t");
                        } else {
                            printf("%6s\t", $contextChar);
                        }
                    } else {
                        printf("%6d\t", $lineNumber);
                    }
                } else {
                    if ($lineNumber) {
                        if ($context && defined $contextChar) {
                            printf("%6d%s\t", $lineNumber, $contextChar);
                        } else {
                            printf("%6d\t", $lineNumber);
                        }
                    } else {
                        if ($context) {
                            if (defined $contextChar) {
                                printf("%s\t", $contextChar);
                            } else {
                                print("\t");
                            }
                        }
                    }
                }
            } else {
                print "    ";
                if ($lineNumber) {
                    printf("%6d%s", $lineNumber, $contextChar);
                }
                if ($self->initialTab) { # must be immediately before matching line
                    print "\t";
                }
            }
            print "$_\n";       # matching line
        }
    }
};
